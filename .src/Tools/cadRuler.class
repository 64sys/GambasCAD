' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA

' Tool maintained by Terco
Inherits ToolsBase
Create Static
Public Const Gender As String = "RULER"

Private Poli As New Float[]
Private fLength As Float
Private fLengthTotal As Float

Public Function Start(Optional ElemToBuild As Variant, Optional _Mode As Integer = 0) As Boolean
    ' Llamada por FCAD al inicio de la aplicacion de esta herramienta

    Dim i As Integer
    ' Aprovechamos para establecer los helpers (comentarios de ayuda en pantalla)
    Me.PointsDone = 0
    Me.PointsTotal = 1e10

    DrawingAids.CmdLineHelper = "Ruler : " & ("mark points")
    poli.Clear
    Poli.Add(0)
    Poli.Add(0)

    Me.Active = True
    gcd.ToolActive = True

End

Public Function MouseUp()

    Dim i As Integer

    If Mouse.Left Then

        Inc Me.PointsDone

        Me.SelEndXr = gcd.Near(gcd.Xreal(mouse.x))
        Me.SelEndyr = gcd.Near(gcd.Yreal(mouse.Y))

        Poli[Poli.max - 1] = Me.SelEndXr
        Poli[Poli.max] = Me.SelEndYr
        Poli.Add(Me.SelEndXr)
        Poli.Add(Me.SelEndYr)
    Else

        Me.Finish
    End If

End

Public Function MouseMove()

    If Poli.Count = 0 Then Return
    Me.SelEndXr = gcd.Near(gcd.Xreal(mouse.X))
    Me.SelEndyr = gcd.Near(gcd.Yreal(mouse.Y))
    Poli[Poli.max - 1] = Me.SelEndXr
    Poli[Poli.max] = Me.SelEndYr

    If Me.PointsDone >= 1 Then

        fLengthTotal = puntos.PoligonLength(poli)

        fLength = puntos.distancia(poli[poli.max - 3], poli[poli.max - 2], poli[poli.max - 1], poli[poli.max])
        DrawingAids.CmdLineHelper = ("Last trame") & " = " & Format(fLength, Config.FormatInquiries) & " - " & ("Total length") & " = " & Format(fLengthTotal, Config.FormatInquiries)

    Endif

    gcd.Redraw
    ' fMain.regen

End

Public Sub Draw()               ' called by gcd.CurrDrawing.drwArea_Draw

    glPlus.PolyLines(Poli, Color.DarkBlue, Color.RGB(0, 255, 100, 200))

End



Public Sub Finish()

    Dim i As Integer, ip As Integer, Prot As New Float[]

    If poli.Count > 6 Then poli.Resize(poli.Count - 2)
    fLengthTotal = puntos.PoligonLength(poli)

    gcd.debugInfo(("Last trame") & " = " & Format(fLength, Config.FormatInquiries),, True)
    gcd.debugInfo(("Total length") & " = " & Format(fLengthTotal, Config.FormatInquiries),, True)
    gcd.clsJobPrevious = Me
    gcd.clsJob = cadSelection
    clsEntities.DeSelection()
    gcd.redraw
    Me.Active = False
    gcd.ToolActive = False

End
