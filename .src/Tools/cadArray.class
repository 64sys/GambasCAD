' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA

' Copies entities n x m times or polar n times
' Tool maintained by Terco
Inherits ToolsBase
Create Static
Public Const Gender As String = "ARRAY"

Public Nx As Integer
Public Ny As Integer
Public Dx As Float
Public Dy As Float

Public Np As Integer
Public Dp As Float

' Add more if needed

Public Function Start(Optional ElemToBuild As Variant, Optional _mode As Integer = 0) As Boolean
    ' Llamada por FCAD al inicio de la aplicacion de esta herramienta

    ' Aprovechamos para establecer los helpers (comentarios de ayuda en pantalla)
    Me.PointsDone = 0
    ' 0 = elegir entidades
    ' 1 = elegir tipo de array, Ortogonal o Polar
    ' 2 = elegir

    Me.PointsTotal = 2                 ' orto: Punto base + Nx + Dx + Ny + Dy   , polar = Punto base + N + angulo

    ny = 1
    nx = 1
    np = 1
    dx = 0
    dy = 0
    dp = 0

    Me.mode = _mode

    ' chequeamos si hubo seleccion previa
    If gcd.Drawing.Sheet.EntitiesSelected.Count = 0 Then                                     ' we have no elements in the selection

        gcd.clsJobCallBack = Me
        gcd.clsJob = cadSelection

        cadSelection.ReturnOnFirstSelection = False
        gcd.clsJob.start()

    Else
        '    DrawingAids.Helper.texto = DrawingAids.CmdLineHelper & ": " & ("Base point")
        Me.Prompt = ("Orthogonal or polar") & " [o]"
        Inc Me.PointsDone
        Me.Prompt &= Str(gcd.Drawing.Sheet.EntitiesSelected.count) & ("elements")

    Endif
    gcd.Drawing.Sheet.GlSheet.PopupMenu = ""
    fmain.PopupMenu = ""

    Me.Active = True

End

'' cadSelection o algun otro proceso nos devuelve el control
Public Sub Run()

    Dim e As Entity

    Me.SelEndXr = cadSelection.SelEndXr ' gcd.Xreal(mouse.x)
    Me.SelEndyr = cadSelection.SelEndyr 'gcd.Yreal(mouse.y)
    Select Case Me.PointsDone
        Case 0  ' estamos recolectando entidades

            ' recolecto las tijeras
            If gcd.Drawing.Sheet.EntitiesSelected.Count = 0 Then ' insisto
                gcd.clsJobCallBack = Me
                gcd.clsJob = cadSelection
                cadSelection.ReturnOnFirstSelection = False
            Else
                '    DrawingAids.Helper.texto = DrawingAids.CmdLineHelper & ": " & ("Base point")
                Me.Prompt &= ("Orthogonal or polar") & " [o] "
                Inc Me.PointsDone
                Me.Prompt &= Str(gcd.Drawing.Sheet.EntitiesSelected.count) & ("elements") & " " & ("selected")

            End If
        Case 1

    End Select

End

Public Function KeyText(EnteredText As String)

    If Me.PointsDone = 1 Then                     ' estamos esperando que nos responda si quere Ortogonal o Polar

        If (EnteredText = "") Or (Left$(UCase$(EnteredText), 1) = "O") Then                        ' enter es Ortogonal

            Me.mode = 1
            Inc Me.PointsDone
            Me.Prompt &= ": " & ("Columns") & " [1]"
        Else If Left$(UCase$(EnteredText), 1) = "P" Then
            Me.mode = 2
            Inc Me.PointsDone
            Me.Prompt &= ": " & ("Repetitions") & " [1]"
        Else

            DrawingAIds.ErrorMessage = ("Bad input")
        End If

    Else If Me.PointsDone = 2 Then

        If Me.mode = 1 Then                             ' ortog->Nx
            Try Nx = CInt(EnteredText)
            If Nx = 0 Then nx = 1
            Inc Me.PointsDone
            If nx > 1 Then
                Me.Prompt &= ": " & ("Distance") & " X=[0]"
            Else
                Me.Prompt &= ": " & ("Rows") & " [1]"
                Inc Me.PointsDone
            End If

        Else                                         ' polar -> N
            Try Np = CInt(EnteredText)
            If np = 0 Then np = 1
            Inc Me.PointsDone
            Me.Prompt &= ": " & ("Angle") & " [45]"
        End If

    Else If Me.PointsDone = 3 Then

        If Me.mode = 1 Then                             ' ortog->Dx
            Try Dx = CFloat(EnteredText)
            Inc Me.PointsDone
            Me.Prompt &= ": " & ("Rows") & " [1]"
        Else                                         ' polar -> N
            Try Dp = Rad(CFloat(EnteredText))
            If dp = 0 Then dp = Rad(45)
            Me.Finish

        End If
    Else If Me.PointsDone = 4 Then

        ' ortog->Ny
        Try Ny = CInt(EnteredText)
        If Ny = 0 Then ny = 1
        Inc Me.PointsDone
        If ny > 1 Then
            Me.Prompt &= ": " & ("Distance") & " Y=[0]"
        Else
            Inc Me.PointsDone
            Me.Finish
        End If

    Else If Me.PointsDone = 5 Then

        ' ortog->Dy
        Try Dy = CFloat(EnteredText)
        Inc Me.PointsDone
        Me.Finish

    End If

    ' catch
    gcd.redraw
    DrawingAIds.ErrorMessage = ("Bad input")

End

Public Sub Finish()

    Dim Rx, Ry As Integer, o As Entity, newO As Entity
    Dim cToRegen As New Collection

    gcd.Drawing.uUndo.OpenUndoStage("Array", Undo.TypeCreate)
    If Me.mode = 1 And Me.PointsDone = 6 Then               ' me.mode orto finished correctly

        For Rx = 0 To Nx - 1

            For rY = 0 To Ny - 1

                If rx = 0 And ry = 0 Then Continue  ' evitamos duplicar la entidad inicial

                For Each o In gcd.Drawing.Sheet.EntitiesSelected

                    newO = clsEntities.ClonEntity(o)               ' devuelve una entidad en exactamente la misma posicion

                    Gcd.CCC[newO.gender].translate(newO, dx * rx, dy * ry)
                    Gcd.CCC[newO.gender].Finish(newo)
                    clsEntities.glGenDrawList(newo)
                    gcd.Drawing.Sheet.Entities.Add(newO, newO.Id)
                    gcd.Drawing.uUndo.AddUndoItem(newo)
                Next
            Next

        Next

    End If

    If Me.mode = 2 And Me.PointsDone = 3 Then               ' me.mode polar finished correctly

        For rY = 2 To Np

            For Each o In gcd.Drawing.Sheet.EntitiesSelected
                newO = clsEntities.ClonEntity(o)               ' devuelve una entidad en exactamente la misma posicion
                Gcd.CCC[newO.gender].translate(newO, -Me.SelStartXr, -Me.SelStartYr)
                Gcd.CCC[newO.gender].rotate(newO, dp * (ry - 1))
                Gcd.CCC[newO.gender].translate(newO, Me.SelStartXr, Me.SelStartYr)
                Gcd.CCC[newO.gender].Finish(newo)
                clsEntities.glGenDrawList(newo)
                gcd.Drawing.Sheet.Entities.Add(newO, newO.Id)
                gcd.Drawing.uUndo.AddUndoItem(newo)
            Next

        Next

    End If
    gcd.Drawing.uUndo.CloseUndoStage()
    Super.Finish()

End
