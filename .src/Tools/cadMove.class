' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA

' Tool maintained by Terco
Inherits ToolsBase
Create Static
Public Const Gender As String = "MOVE"

Public Function Start(Optional ElemToBuild As Variant, Optional _Mode As Integer = 0) As Boolean
    ' Llamada por FCAD al inicio de la aplicacion de esta herramienta

    ' Aprovechamos para establecer los helpers (comentarios de ayuda en pantalla)
    Dim i As Integer, E As Entity

    Me.PointsDone = 0
    Me.PointsTotal = 2

    ' this affects the gl Drawing list

    If gcd.Drawing.EntitiesSelected.Count = 0 Then                                     ' we have no elements in the selection

        DrawingAids.CmdLineHelper = ("Move") & ": " & ("Select entities")

        gcd.clsJobPrevious = Me
        gcd.clsJob = cadSelection

    Else                                                                    ' need the first point

        'DrawingAids.CmdLineHelper = DrawingAids.CmdLineHelper & ": " & ("Base point")
        DrawingAids.CmdLineHelper = ("Move") & " " & Str(gcd.Drawing.EntitiesSelected.Count) & " " & ("elements") & ": " & ("Base point")

        ' armamos el menu contextual y lo asignamos a la GL area
        gcd.Drawing.GLAreaInUse.PopupMenu = Me.Gender



        gcd.SnapMode = Config.SnapModeSaved                 ' vuelvo al Snap Guardado

        clsEntities.GLGenDrawListSel(0)                     ' genero drawing lists
        clsEntities.GLGenDrawListAll(True)

        gcd.Drawing.uUndo.OpenUndoStage("Move entities", Undo.TypeModify)
        For Each E In gcd.Drawing.EntitiesSelected
            gcd.Drawing.uUndo.AddUndoItem(clsEntities.ClonEntity(E, False))
        Next
        gcd.Redraw

    Endif
    Me.Active = True
    gcd.ToolActive = True

End

' Public Function NewParameter(vParam As Variant[], Optional Definitive As Boolean = False) As Boolean
'
'     If vParam[0] = "point" Then
'
'         ' enganio al evento mouse
'         MouseMove(gcd.Xpix(vParam[1]), gcd.Ypix(vParam[2]))
'         MouseUp(gcd.Xpix(vParam[1]), gcd.Ypix(vParam[2]))
'
'     Endif
'
' End

Public Function MouseUp()

    If Mouse.Right Then
        Me.Cancel()

    Else If Mouse.Left Then
        Inc Me.PointsDone

        If Me.PointsDone = 1 Then ' acabo de marcar el punto base
            ' volvemos el Snap al original
            gcd.SnapMode = Config.SnapModeSaved

            ' ya tengo cosas seleccionadas
            DrawingAids.Helper.texto = ("Final point")
            DrawingAids.CmdLineHelper = ("Move") & " " & Str(gcd.Drawing.EntitiesSelected.Count) & " " & ("elements") & ": " & ("Final point")

            ' ' punto real base
            ' Me.SelStartXr = gcd.Near(gcd.Xreal(mouse.x))
            ' Me.SelStartYr = gcd.Near(gcd.Yreal(mouse.y))

            ' prevenir acciones tontas como hacer click en otro elemento antes de terminar con el actual
            ' TODO: reparar -> gcd.CurrDrawing.DesHabilitarBotones
        Else If Me.PointsDone = 2 Then ' acabo de marcar el punto destino
            ' permitir otros trabajos
            ' TODO: reparar -> gcd.CurrDrawing.HabilitarBotones

            ' job finished correctly

            ' and now we move
            clsEntities.Move(Me.SelEndXr - Me.SelStartXr, Me.SelEndyr - Me.SelStartYr)

            clsEntities.Finish
            gcd.Drawing.uUndo.CloseUndoStage()

            Me.Finish

        Endif
    End If

End

Public Function MouseMove()


    Dim X, Y As Float
    ' we are waiting for a point

    X = gcd.Near(gcd.Xreal(mouse.x))
    Y = gcd.Near(gcd.Yreal(mouse.y))

    ' yo soy el responsable de chequear POI
    If Not gcd.flgSearchingPOI Then
        gcd.Drawing.iEntity = clsMouseTracking.CheckBestPOI(X, Y)


        If (gcd.Drawing.iEntity[2] > 0) Then

            ' esto re enganchado

            X = gcd.Drawing.iEntity[0]
            Y = gcd.Drawing.iEntity[1]
        End If
    End If


    If Me.PointsDone = 0 Then
        Me.SelStartXr = X
        Me.SelStartYr = Y

    Else If Me.PointsDone = 1 Then
        ' we compare Real values from the starting point to the location
        ' represente by the mouse now, taking account that the drawing
        ' could have being moved by the mouse wheel
        Me.SelEndXr = X
        Me.SelEndYr = Y



        If gcd.Orthogonal Then          ' hablame de operadores logicos

            If Abs(Me.SelEndXr - Me.SelStartXr) > Abs(Me.SelEndyr - Me.SelStartYr) Then ' prevalece X
                Me.SelEndyr = Me.SelStartYr
            Else
                Me.SelEndXr = Me.SelStartXr
            Endif

        End If



        Me.glTranslate[0] = Me.SelEndXr - Me.SelStartXr
        Me.glTranslate[1] = Me.SelEndyr - Me.SelStartYr

    End If
    gcd.Redraw

End
