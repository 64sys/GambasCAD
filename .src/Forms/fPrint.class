' Gambas class file

Public MyPrinter As New Printer
Public cPaperSizes As New Collection

Public snxAreaToPrint As New Single[]

Public Sub btnCAncel_Click()

    gcd.clsJobCallBack = Null

    Me.Close(0)

End

Public Sub Form_Activate()

    ' Obtengo la impresion
    ' frmPrinter.Text = Printer1.Name

    ' es posible que haya vuelto de buscar un area de impresion
    If cadSelection.StartXr > cadSelection.EndXr Then Swap cadSelection.StartXr, cadSelection.EndXr
    If cadSelection.StartYr > cadSelection.EndYr Then Swap cadSelection.StartYr, cadSelection.EndYr

    snxAreaToPrint = New Single[]
    snxAreaToPrint.Add(cadSelection.StartXr)
    snxAreaToPrint.Add(cadSelection.StartYr)
    snxAreaToPrint.Add(cadSelection.EndXr)
    snxAreaToPrint.Add(cadSelection.Endyr)

End

Public Sub Form_Open()

    ' obtengo la lista de impresoras

    Dim stxPrinters As New String[]
    Dim sSize As Variant

    stxPrinters = Printer.List

    cmbPrinter.List = stxPrinters
    cmbPrinter.Add(("Print to PDF file"), 0)

    cPaperSizes.Clear
    cPaperSizes.Add("A0 841mm X 1189mm", "A0")
    cPaperSizes.Add("A1 594mm X 841mm", "A1")
    cPaperSizes.Add("A2 420mm X 594mm", "A2")
    cPaperSizes.Add("A3 297mm X 420mm", "A3")
    cPaperSizes.Add("A4 210mm X 297mm", "A4")
    cPaperSizes.Add("A5 148mm X 210mm", "A5")
    cPaperSizes.Add("Custom size in mm", "CU")

    cmbPaperSize.Clear
    For Each sSize In cPaperSizes
        cmbPaperSize.Add(sSize)
    Next

    cmbScale.Clear
    cmbScale.Add("Custom")
    cmbScale.Add("1000:1")
    cmbScale.Add("200:1")
    cmbScale.Add("100:1")
    cmbScale.Add("75:1")
    cmbScale.Add("50:1")
    cmbScale.Add("20:1")
    cmbScale.Add("10:1")
    cmbScale.Add("1:1")
    cmbScale.Add("1:10")
    cmbScale.Add("1:20")
    cmbScale.Add("1:50")
    cmbScale.Add("1:75")
    cmbScale.Add("1:100")
    cmbScale.Add("1:200")
    cmbScale.Add("1:1000")

    ' set values from current?

End

Public Sub btnPrint_Click()

    ' coloco todo los parametros en el estilo de impresion en uso
    With gcd.Drawing.CurrPrintStyle
        .ColorStyle = -((1 * radGray.Value) + (2 * radColor.Value))

        .PrintScale = vlbScalePaper.Value / vlbScaleDraw.Value  ' para aplicar la escala correcta debe setearse la impresora como "A3 sin margenes" por ej

        If radFitToPage.Value Then .PrintScale = -1
        .PaperSizeW = vlbWidth.Value
        .PaperSizeH = vlbHeight.Value

        ' margenes
        .PrintOffsetX = vlbLeft.Value
        .PrintOffsetY = vlbTop.Value
        If radFitTopLeft.Value Then
            .CenteringStyle = 1
        Else If radFitCenter.Value Then
            .CenteringStyle = 2
        Else If radFitCustom.Value Then
            .CenteringStyle = 0
        End If

        If radLandscape.Value Then .PrintOrientation = Printer.Landscape Else .PrintOrientation = Printer.Portrait

        .PrintInvert = chkInvert.Value
        If Me.radAreaWindow.Value Then .PrintArea = 2
        If Me.radAreaAll.Value Then .PrintArea = 0
        If Me.radAreaView.Value Then .PrintArea = 1

        .PrintAreaX0 = snxAreaToPrint[0]
        .PrintAreaY0 = snxAreaToPrint[1]
        .PrintAreaX1 = snxAreaToPrint[2]
        .PrintAreaY1 = snxAreaToPrint[3]

        'MyPrinter.Print()

    End With

    ' fMain.Printer1.Paper = cmbPaperSize.Text  ' REEMPLAZADO POR LOS SIGUIENTES
    Printer1.PaperHeight = gcd.Drawing.CurrPrintStyle.PaperSizeH
    Printer1.PaperWidth = gcd.Drawing.CurrPrintStyle.PaperSizeW
    Printer1.Orientation = gcd.Drawing.CurrPrintStyle.PrintOrientation

    If cmbPrinter.Index = 0 Then
        Printer1.OutputFile = User.Home &/ "print.pdf"
    Else
        Printer1.OutputFile = ""
        Printer1.Name = cmbPrinter.Text
    End If

    Printer1.Print

End

Public Sub cmbPaperSize_Click()

    Dim sParts As String[]

    If Left(cmbPaperSize.Text, 1) = "A" Then

        vlbHeight.Enabled = False
        vlbWidth.Enabled = False
        lblHeight.Enabled = False
        lblWidth.Enabled = False

        sParts = Split(cmbPaperSize.Text, " ")

        If radPortrait.Value Then
            vlbWidth.Text = Left(sParts[1], -2)
            vlbHeight.Text = Left(sParts[3], -2)
        Else
            vlbWidth.Text = Left(sParts[3], -2)
            vlbHeight.Text = Left(sParts[1], -2)

        End If

    Else
        vlbHeight.Enabled = True
        vlbWidth.Enabled = True
        lblHeight.Enabled = True
        lblWidth.Enabled = True

    Endif

End

Public Sub run()

    Me.ShowModal()

End

Public Sub cmbScale_Click()

    Dim sScales As String[]

    If IsNumber(Left(cmbScale.Text, 1)) Then
        sScales = Split(cmbScale.Text, ":")

        lblScale1.Enabled = False
        lblScale2.Enabled = False
        vlbScaleDraw.Enabled = False
        vlbScalePaper.Enabled = False
        vlbScalePaper.Value = sScales[0]
        vlbScaleDraw.Value = sScales[1]
        radScale.Value = True
    Else
        lblScale1.Enabled = True
        lblScale2.Enabled = True
        vlbScaleDraw.Enabled = True
        vlbScalePaper.Enabled = True
    Endif

End

Public Sub radFitCustom_Click()

    vlbTop.Enabled = True
    vlbLeft.Enabled = True
    lblFit1.Enabled = True
    lblFit2.Enabled = True

End

Public Sub radFit_Click()

    vlbTop.Enabled = False
    vlbLeft.Enabled = False
    lblFit1.Enabled = False
    lblFit2.Enabled = False

End

Public Sub btnPrintWindow_Click()

    Me.radAreaWindow.Value = True
    gcd.clsJobCallBack = Me ' necesitamos volver aca luego de que marque un area
    gcd.clsJob = cadSelection
    gcd.clsJob.start()
    Me.Close()

End

Public Sub radFitToPage_Click()

    radFitTopLeft.Enabled = False
    radFitCenter.Enabled = False
    radFitCustom.Enabled = False
    vlbLeft.Enabled = False
    vlbTop.Enabled = False

End

Public Sub radScale_Click()

    radFitTopLeft.Enabled = True
    radFitCenter.Enabled = True
    radFitCustom.Enabled = True
    vlbLeft.Enabled = True
    vlbTop.Enabled = True

End

Public Sub printer1_Begin()

    gcd.debuginfo("Printing")

End

Public Sub printer1_Paginate()

    Printer1.FullPage = True
    Printer1.Count = 1
    gcd.debuginfo("Paginating")

End

Public Sub printer1_End()

    gcd.debuginfo("Print done")
    gcd.clsJobCallBack = Null

    Me.Close()

End

Public Sub Printer1_Draw()

    Dim flxLimits As New Float[]
    Dim scaleX, scaleY As Float
    Dim PSY As PrintStyle
    Dim dX, dY, sX, sY As Float

    gcd.debuginfo("Printing pages")
    '  Dim jPrint As New JSONCollection
    PSY = gcd.Drawing.CurrPrintStyle

    ' DETERMINO LO QUE HAY QUE IMPRIMIR
    If PSY.PrintArea = 0 Then ' all drawing

        flxLimits = clsEntities.ComputeLimits(gcd.Drawing.oEntities)

        PSY.PrintAreaX0 = flxLimits[0]
        PSY.PrintAreaY0 = flxLimits[1]
        PSY.PrintAreaX1 = flxLimits[2]
        PSY.PrintAreaY1 = flxLimits[3]

    Else If PSY.PrintArea = 1 Then ' viewport

        PSY.PrintAreaX0 = gcd.Xreal(0)
        PSY.PrintAreaY0 = gcd.Yreal(0)
        PSY.PrintAreaX1 = gcd.Xreal(fmain.GLArea1.Width)
        PSY.PrintAreaY1 = gcd.Yreal(fmain.GLArea1.h)

    Else

        ' nada porque ya estan seteados

    End If

    ' '==============================TEST 1==========================================
    ' ' De no mediar escalado y centrado, la impresion ocurrente entre
    '
    ' '   0,0 +-------------------------------+
    ' '       |                               |
    ' '       |                               |
    ' '       |                               |
    ' '       |                               |
    ' '       |                               |
    ' '       +-------------------------------+ Paint.W, Paint.H (ambos positivos)
    '
    ' '   Pero en OpenGL (y por ende las coordenadas de los dibujos)
    ' '       +-------------------------------+ Xmax,Ymax
    ' '       |                               |
    ' '       |                               |
    ' '       |            0;0                |
    ' '       |                               |
    ' '       |                               |
    ' '       +-------------------------------+
    '      -Xmin,-Ymin

    '==============================ACTUAL PRINT==========================================

    Paint.Reset ' vuelvo escalas y traslados a cero

    ' APLICO LA ESCALA

    If PSY.PrintScale = -1 Then ' fit to page

        Paint.Translate(Paint.W / 2, Paint.H / 2) ' centro el dibujo
        ' trato de meter centrado
        If (PSY.PrintAreaX1 - PSY.PrintAreaX0) > 1e-10 Then scaleX = Paint.w / (PSY.PrintAreaX1 - PSY.PrintAreaX0) Else scaleX = 1e10
        If (PSY.PrintAreaY1 - PSY.PrintAreaY0) > 1e-10 Then scaleY = Paint.H / (PSY.PrintAreaY1 - PSY.PrintAreaY0) Else scaleY = 1e10

        If ScaleX < ScaleY Then gcd.PrintingScale = ScaleX Else gcd.PrintingScale = Scaley
        Paint.Scale(gcd.PrintingScale * 0.85, -gcd.PrintingScale * 0.85)

        ' centro el dibujo
        Paint.Translate(-(PSY.PrintAreaX1 + PSY.PrintAreaX0) / 2, -(PSY.PrintAreaY1 + PSY.PrintAreaY0) / 2)
    Else ' tengo que aplicar cierta escala y ciertos margenes

        'Paint.Translate(0, -Paint.H) '
        ' Escala basica de impresion, luego de esto, la escala a aplicar seran 1 unidad=1 m
        sX = Paint.Width / Printer1.PaperWidth
        sY = Paint.Height / Printer1.PaperHeight
        ' primero aplico la escala, Unidades de dibujo:Metros
        gcd.PrintingScale = PSY.PrintScale * 1000 ' para pasar de mm a metros
        sX *= gcd.PrintingScale
        sY *= gcd.PrintingScale

        ' ahora los margenes solicitados

        If PSY.CenteringStyle = 0 Then 'custom
            Paint.Translate(0, Paint.H) ' centro el dibujo
            Paint.Scale(sX, -sY)
            dX = -PSY.PrintAreaX0
            dY = -PSY.PrintAreaY0

            dX += PSY.PrintOffsetX * gcd.PrintingScale
            dY += PSY.PrintOffsetY * gcd.PrintingScale

        Else If PSY.CenteringStyle = 1 Then ' top left
            'Paint.Translate(-PSY.PrintAreaX0 * gcd.PrintingScale, -PSY.PrintAreaY0 * gcd.PrintingScale)
            Paint.Translate(0, Paint.H) ' centro el dibujo
            Paint.Scale(sX, -sY)

            dX = -PSY.PrintAreaX0
            dY = -PSY.PrintAreaY0

        Else If PSY.CenteringStyle = 2 Then ' centered
            Paint.Translate(Paint.w / 2, Paint.H / 2) ' centro el dibujo
            Paint.Scale(sX, -sY)

            dx = -(PSY.PrintAreaX1 + PSY.PrintAreaX0) / 2
            dy = -(PSY.PrintAreaY1 + PSY.PrintAreaY0) / 2

        Endif
        Paint.Translate(dX, dY)

    End If

    Dim L As Layer, E, E2 As Entity
    For Each L In gcd.Drawing.oLayers
        If L.Visible Then
            For Each E2 In L.Entities
                If Not e2.Visible Then Continue
                If PSY.PrintArea <> 0 Then E = clsEntities.TrimmedEntity(E2, PSY.PrintAreaX0, PSY.PrintAreaY0, PSY.PrintAreaX1, PSY.PrintAreaY1) Else e = e2
                If IsNull(e) Then Continue
                If e.LineWidth = 0 Then e.LineWidth = 1
                ' If jPrint.Exist("Monochrome") Then
                '   If jPrint["Monochrome"] Then
                '     e.colour = 255
                '   Endif
                ' Endif
                Paint.Brush = Paint.Color(Color.Black)

                ' TODO: aplicar las puntas segun color de la entidad
                Paint.LineWidth = 0.15 / gcd.PrintingScale  ' punta 0.15mm
                Paint.Save  ' guardamos la matriz de transformacion por si la entidad la modifica
                If InStr("TEXT MTEXT ATTRIB ATTDEF", e.Gender) Then
                    paint.Font.Size = Abs(e.fParam[cadText.ipaTextHeight] / gcd.PrintingScale * 2)   ' TODO: verificar
                End If

                gcd.CCC[e.gender].draw2(e)
                Paint.Restore ' restauramos la matriz
            Next
        End If
    Next

End

Public Sub radLandscape_Click()

    cmbPaperSize_Click

End

Public Sub radPortrait_Click()

    cmbPaperSize_Click

End
